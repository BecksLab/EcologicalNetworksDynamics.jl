# How to generate model parameters?

Once the [`Foodweb`](@ref) is created,
you still have to attribute values to the system parameters.
To navigate easily through the parameters,
they are split into a number of components needed:

- [`BodyMass`](@ref) contains the species body masses
- [`MetabolicClass`](@ref) contains the species metabolic classes (among producers, invertebrates and ectotherms)
- [`FunctionalResponse`](@ref) contains the functional response, which can be of three types: bioenergetic, classic or linear
- [`ProducerGrowth`](@ref) contains the growth function of the producers, which can be logictic or based on nutrients consumptions (when nutrients are explicitly present in the model)
- [`Metabolism`](@ref) contains the species metabolic rates
- [`Mortality`](@ref) contains the species mortality rates

In addition, one can define a dependency on temperature and add different types of interactions:
- [`Temperature`](@ref) contains a value of the temperature of the environment (in Kelvin). If a value for it is given, the species biorates depending on temperature are all automatically updated
-  [`NonTrophicLayers`](@ref) contains possible additional types of interactions (or layers, in addition to feeding) between species 

** updated until here**

All the fields are stored within the same object [`ModelParameters`](@ref).
In addition to the 4 above fields,
[`ModelParameters`](@ref) stores also the [`FoodWeb`](@ref).

By default, the model parameters can be simply generated by providing only the foodweb.

```@setup econetd
using EcologicalNetworksDynamics
```

```@example econetd
foodweb = FoodWeb([0 0 0; 1 0 0; 0 1 0]);
params = ModelParameters(foodweb)
```

However, you may want to customize the model parameters to fit your needs.
We explain how to do so for each of the 3 fields in the following sections.

## Biological rates

[`BioRates`](@ref) contains the species metabolic demand (`x`)
and the species intrinsic growth rates (`r`).

```@example econetd
foodweb = FoodWeb([0 0 0; 1 0 0; 0 1 0]; Z = 10);
biorates = BioRates(foodweb)
biorates.x # metabolic demand
biorates.r # intrinsic growth rate
```

If you want to change the default rate values you can specify your own rate vector.

```@example econetd
x_custom = [1, 2, 3];
r_custom = [4, 5, 6];
biorates = BioRates(foodweb; x = x_custom, r = r_custom);
biorates.x
biorates.r
```

In case you want to attribute the same rate value for every species,
instead of giving a vector filled with a constant,
you can directly pass the constant.

```@example econetd
biorates = BioRates(foodweb; x = 1, r = 2);
biorates.x
biorates.r
```

By default these rates follow an allometric scaling,
*i.e.* the rate ``\rho`` depends on the species body-mass (``M``)
such that: ``\rho = aM^b`` with ``a`` and ``b`` the *allometric parameters*.

``a`` and ``b`` depend first on the rate,
for instance they take different value for the growth rate and the metabolic demand.
Moreover, they also depend on the species metabolic class,
if computing the metabolic demand,
the value of ``a`` and ``b`` will be different for vertebrates and invertebrates.
These values of ``a`` and ``b`` for the different metabolic classes
are stored in [`AllometricParams`](@ref) objects.
You can access allometric parameters values taken from literature
for the intrinsic growth rate and the metabolic demand.

```@example econetd
DefaultGrowthParams() # default a, b for intrinsic growth rate
DefaultMetabolismParams() # default a, b for intrinsic metabolic demand
```

But you can also define your own allometric parameters.

```@example econetd
my_allometric_params = AllometricParams(1, 2, 3, 4, 5, 6)
```

And give them with the foodweb to [`allometric_rate`](@ref),
which will compute the vector of the species rates:

!!! note
    
    The foodweb is provided to get the species trophic levels.

```@example econetd
foodweb = FoodWeb([0 0 0; 1 0 0; 0 1 0]; Z = 10)
rate = allometric_rate(foodweb, my_allometric_params) # 1 value per species
```

!!! note
    
    If you change the metabolic class of the consumers,
    the corresponding rates will change too.
    
    ```julia-repl
    julia> foodweb.metabolic_class = ["producer", "ectotherm vertebrate", "ectotherm vertebrate"];
    
    julia> rate = allometric_rate(foodweb, my_allometric_params)
    3-element Vector{Float64}:
     1.0
     2.0e5
     2.0e10
    ```

Then the `rate` vector computed with your custom allometric parameters
can be given to [`BioRates`](@ref) as seen previously.

```@example econetd
biorates = BioRates(foodweb; x = rate) # custom metabolic demand
```

## Producer growth

Two models are available for producers growth:

  - a logistic growth model, used by default, and a
  - a nutrient intake model.

In both models, the producer biomasses $B_p$ grow following the general equation:

$$
growth = r_i * B_i * G_i
$$

Where $r_i$ is producer $i$'s intrinsic growth rate, $B_i$ is its biomass
and $G_i$ is its net growth term.
The latter can take different forms depending on the growth model in use.

The form of producer growth term is controlled by
the field `producer_growth` in model parameters.

### The logistic model

In the logistic model, $G_i$ takes the general form:

$$
G_i = 1 - \frac{s}{K_i}
$$

Where the numerator $s$ can take the simplest form $s = B_i$ (default),
or include a matrix $a$ describing the relative strength of the inter- vs
intra-specific competition among producers.
In this case, $s = \sum a_{ij}B_j$.

The matrix $a$ should have as many rows
and columns as there are producers in the food web.
When the intra-specific competition is $1$ ($a_{ii} = 1$),
then species pairs with values below $1$ ($a_{ij} < 1$) describes something akin to
facilitation and values above $1$ ($a_{ij} > 1$) describe stronger inter-specific
competition (that would lead to competitive exclusion in the absence of other processes).
Note that when all non-diagonal values are set to $0$,
then $s = \sum\alpha_{ij}B_j = B_j$ and the two models are equivalent.

When setting up the simulations, the logistic model is controlled as followed:

```
julia> foodweb = FoodWeb([0 0 0; 0 0 0; 1 1 0])
FoodWeb of 3 species:
  A: sparse matrix with 2 links
  M: [1.0, 1.0, 1.0]
  metabolic_class: 2 producers, 1 invertebrates, 0 vertebrates
  method: unspecified
  species: [s1, s2, s3]

julia> logmod = LogisticGrowth(foodweb) # Default behaviour.
LogisticGrowth:
  Kᵢ - carrying capacity: [1.0, 1.0, nothing]
  a - competition: (3, 3) sparse matrix

julia> #Note: This is equivalent to ModelParameters(foodweb):
julia> p = ModelParameters(foodweb, producer_growth = logmod)
ModelParameters{BioenergeticResponse, LogisticGrowth}:
  network: FoodWeb(S=3, L=2)
  environment: Environment(K=[1, 1, nothing], T=293.15K)
  biorates: BioRates(d, r, x, y, e)
  functional_response: BioenergeticResponse
  producer_growth: LogisticGrowth
  temperature_response: NoTemperatureResponse
```

which allows for control over the value of the carrying capacities
and the matrix of competition:

```
julia> logmod = LogisticGrowth(foodweb, K = [10, 5, nothing], αij = 0.8)
LogisticGrowth:
  Kᵢ - carrying capacity: [10.0, 5.0, nothing]
  a - competition: (3, 3) sparse matrix
```

### The nutrient intake model

The nutrient intake model is implemented following Brose et al., 2005.
In this model, the net growth terms depends on nutrient $l$'s concentration ($N_l$)
and take the form:

$$
G_i(N) = MIN(\frac{N_l}{K_{li} + N_l}, ...)
$$

where $K_{li}$ describes the species-specific half saturation densities
for each nutrient $l$.
In the absence of other factors it describes the producers hierarchy of competition
(lower values means higher intake efficiency).

The nutrients concentrations are described as:

$$
dN_l/dt = D(S_l - N_l)-\sum^n_{i = 1}(C_{li}G_i(N)B_i)
$$

where $D$ is the system turnover (default is 0.25), $S_l$ is
nutrient $l$'s supply concentration and $C_{li}$ is the concentration of
the nutrients in the producers biomass
(the higher it is the more essential the nutrient is for the species growth).

As with the logistic model, the nutrient intake model's behaviour is controlled
through the use of the `producer_growth` field:

```
julia> foodweb = FoodWeb([0 0 0; 0 0 0; 1 1 0])
FoodWeb of 3 species:
  A: sparse matrix with 2 links
  M: [1.0, 1.0, 1.0]
  metabolic_class: 2 producers, 1 invertebrates, 0 vertebrates
  method: unspecified
  species: [s1, s2, s3]

julia> ni2 = NutrientIntake(foodweb)
NutrientIntake - 2 nutrients:
  D - turnover rate: 0.25
  Sₗ - supply concentrations: [10.0, 10.0]
  Cₗᵢ - relative contents: (S, n) sparse matrix
  Kₗᵢ - half sat. densities: (S, n) matrix
```

By default, the models has 2 nutrients, but as for every other parameters,
this can be changed:

```
julia> ni4 = NutrientIntake(foodweb, n = 4)
NutrientIntake - 4 nutrients:
  D - turnover rate: 0.25
  Sₗ - supply concentrations: [10.0, 10.0, ..., 10.0, 10.0]
  Cₗᵢ - relative contents: (S, n) sparse matrix
  Kₗᵢ - half sat. densities: (S, n) matrix
```

This is then passed to `ModelParameter`:

```
julia> p = ModelParameters(foodweb, producer_growth = ni2)
ModelParameters{BioenergeticResponse, NutrientIntake}:
  network: FoodWeb(S=3, L=2)
  environment: Environment(K=[1, 1, nothing], T=293.15K)
  biorates: BioRates(d, r, x, y, e)
  functional_response: BioenergeticResponse
  producer_growth: NutrientIntake
  temperature_response: NoTemperatureResponse
```

## Environment

[`Environment`](@ref) contains the temperature of the system.
As for the biological rates,
environmental variables can be generated by only providing the foodweb,
in which case the parameters take default values.

```@example econetd
foodweb = FoodWeb([0 0 0; 0 0 0; 0 1 0]; Z = 10)
environment = Environment()
```

By default the temperature is set to 293.15 K.

```@example econetd
environment.T
```

However, these default values can be changed
by providing custom values to the [`Environment`](@ref) method.
For the temperature
you just have to provide the scalar corresponding to the wanted temperature.

```@example econetd
environment = Environment(; T = 273.15);
environment.T
```

## Functional responses

You can choose between 3 functional responses:
[Linear response](@ref), [Bioenergetic response](@ref) and [Classic response](@ref).
By default the [Bioenergetic response](@ref) response is selected.

```@example econetd
params = ModelParameters(foodweb);
params.functional_response
```

However, if you want you can choose another functional response by giving it as an argument.
For the classic response, do:

```@example econetd
classic_response = ClassicResponse(foodweb); # define your response
params = ModelParameters(foodweb; functional_response = classic_response);
params.functional_response
```

And for the linear response, do:

```@example econetd
linear_response = LinearResponse(foodweb); # define your response
params = ModelParameters(foodweb; functional_response = linear_response);
params.functional_response
```

For more information about functional responses,
see [How to choose the functional response?](@ref functional_response)
or the help for [`LinearResponse`](@ref), [`BioenergeticResponse`](@ref)
and [`ClassicResponse`](@ref)
