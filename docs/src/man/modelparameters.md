# How to generate model parameters?

Once the [`FoodWeb`](@ref) is created,
you still have to attribute values to the system parameters.
To navigate easily through the parameters,
they are split in 3 different fields depending on their nature:

  - [`BioRates`](@ref) contains species biological rates,
    *e.g.* producer intrinsic growth rates
  - [`Environment`](@ref) contains environmental variables,
    *e.g.* producer carrying capacities
  - [`FunctionalResponse`](@ref) contains functional response parameters,
    *e.g.* the hill exponent

All the fields are stored within the same object [`ModelParameters`](@ref).
In addition to the 3 above fields,
[`ModelParameters`](@ref) stores also the [`FoodWeb`](@ref).

By default, the model parameters can be simply generated by providing only the foodweb.

```@setup befwm2
using BEFWM2
```

```@example befwm2
foodweb = FoodWeb([0 0 0; 1 0 0; 0 1 0]);
params = ModelParameters(foodweb)
```

However, you may want to customize the model parameters to fit your needs.
We explain how to do so for each of the 3 fields in the following sections.

## Biological rates

[`BioRates`](@ref) contains the species metabolic demand (`x`)
and the species intrinsic growth rates (`r`).

```@example befwm2
foodweb = FoodWeb([0 0 0; 1 0 0; 0 1 0]; Z = 10);
biorates = BioRates(foodweb)
biorates.x # metabolic demand
biorates.r # intrinsic growth rate
```

If you want to change the default rate values you can specify your own rate vector.

```@example befwm2
x_custom = [1, 2, 3];
r_custom = [4, 5, 6];
biorates = BioRates(foodweb; x = x_custom, r = r_custom);
biorates.x
biorates.r
```

In case you want to attribute the same rate value for every species,
instead of giving a vector filled with a constant,
you can directly pass the constant.

```@example befwm2
biorates = BioRates(foodweb; x = 1, r = 2);
biorates.x
biorates.r
```

By default these rates follow an allometric scaling,
*i.e.* the rate ``\rho`` depends on the species body-mass (``M``)
such that: ``\rho = aM^b`` with ``a`` and ``b`` the *allometric parameters*.

``a`` and ``b`` depend first on the rate,
for instance they take different value for the growth rate and the metabolic demand.
Moreover, they also depend on the species metabolic class,
if computing the metabolic demand,
the value of ``a`` and ``b`` will be different for vertebrates and invertebrates.
These values of ``a`` and ``b`` for the different metabolic classes
are stored in [`AllometricParams`](@ref) objects.
You can access allometric parameters values taken from literature
for the intrinsic growth rate and the metabolic demand.

```@example befwm2
DefaultGrowthParams() # default a, b for intrinsic growth rate
DefaultMetabolismParams() # default a, b for intrinsic metabolic demand
```

But you can also define your own allometric parameters.

```@example befwm2
my_allometric_params = AllometricParams(1, 2, 3, 4, 5, 6)
```

And give them with the foodweb to [`allometric_rate`](@ref),
which will compute the vector of the species rates:

!!! note
    
    The foodweb is provided to get the species trophic levels.

```@example befwm2
foodweb = FoodWeb([0 0 0; 1 0 0; 0 1 0]; Z = 10)
rate = allometric_rate(foodweb, my_allometric_params) # 1 value per species
```

!!! note
    
    If you change the metabolic class of the consumers,
    the corresponding rates will change too.
    
    ```julia-repl
    julia> foodweb.metabolic_class = ["producer", "ectotherm vertebrate", "ectotherm vertebrate"];
    
    julia> rate = allometric_rate(foodweb, my_allometric_params)
    3-element Vector{Float64}:
     1.0
     2.0e5
     2.0e10
    ```

Then the `rate` vector computed with your custom allometric parameters
can be given to [`BioRates`](@ref) as seen previously.

```@example befwm2
biorates = BioRates(foodweb; x = rate) # custom metabolic demand
```

## Environment

[`Environment`](@ref) contains the species carrying capacities
and the temperature of the system.
As for the biological rates,
environmental variables can be generated by only providing the foodweb,
in which case the parameters take default values.

```@example befwm2
foodweb = FoodWeb([0 0 0; 0 0 0; 0 1 0]; Z = 10)
environment = Environment(foodweb)
```

By default carrying capacities (K) are set to 1 for producers and 0 for consumers
as their growth term is assumed to be null.

```@example befwm2
environment.K
```

And the temperature is set to 293.15 K.

```@example befwm2
environment.K
```

However, these default values can be changed
by providing custom values to the [`Environment`](@ref) method.
For the temperature
you just have to provide the scalar corresponding to the wanted temperature.

```@example befwm2
environment = Environment(foodweb; T = 273.15);
environment.T
```

And to change the carrying capacities you can either provide:

  - a vector containing one value for each species
  - a scalar if you want to set all the producer carrying capacities to the
    same value

```@example befwm2
environment = Environment(foodweb; K = [1, 2, 0]); # using a vector
environment.K

environment = Environment(foodweb; K = 2); # using a scalar
environment.K
```

## Functional responses

You can choose between 3 functional responses:
[Linear response](@ref), [Bioenergetic response](@ref) and [Classic response](@ref).
By default the [Bioenergetic response](@ref) response is selected.

```@example befwm2
params = ModelParameters(foodweb);
params.functional_response
```

However, if you want you can choose another functional response by giving it as an argument.
For the classic response, do:

```@example befwm2
classic_response = ClassicResponse(foodweb); # define your response
params = ModelParameters(foodweb; functional_response = classic_response);
params.functional_response
```

And for the linear response, do:

```@example befwm2
linear_response = LinearResponse(foodweb); # define your response
params = ModelParameters(foodweb; functional_response = linear_response);
params.functional_response
```

For more information about functional responses,
see [How to choose the functional response?](@ref)
or the help for [`LinearResponse`](@ref), [`BioenergeticResponse`](@ref)
and [`ClassicResponse`](@ref)
